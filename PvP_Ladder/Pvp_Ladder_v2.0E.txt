//===== EinherjarRo Scripts ==================================
//= PvP Ladder
//===== Author ===============================================
//= Stolao (Original Author)
//= Happy (2.0)
//= Stolao (2.01+)
//===== Current Version: =====================================
//= 2.0E
//===== Compatible With: =====================================
//= rAthena SVN, Hercules, 3ceam, eAthena
//===== Description: =========================================
// Configurable Pvp Ladder
//===== Comments: ============================================
//= [Stolao]
// Needs bug testing, please report any bugs
// Unable to test thurally on my comp atm
//= [Happy]
// Update* I tested it and found tons of bugs, I'm in rush so
// I fixed them myself. It is now working 100%.
// The version is unofficial version. Sorry Stolao I fixed it
// Without your permission XD
//===== Additional Comments: =================================
//= 1.00 Initial Release
//= 1.01 Added Location with veiwing of ladder
//= 1.02 Added GM Menu
//= 1.03 Added atcommand
//= 1.04 Shortend menu script
//= 1.05 Added PvpPoints into confug and script
//= 1.06 Added Abuse Protection
//= 1.07 Made Pvp Points Configurable
//= 1.08 Fixed typo in Reset Option
//= 1.09 Made Amount of pvp points earned configurable
//= 1.0A Ajusted Spacing for easier reading
//= 1.0B Added Warp to savepoint on abuse into config
//= 2.00 Fixed bugs on ranking modification
//= 2.01 Fixed Tabulation
//= 2.02 Added Death Counter Option
//= 2.03 Small text change in announcement
//= 2.04 Added reset death counter 
//= 2.05 Fixed typos
//= 2.06 Fixed syntec error
//= 2.07 Fixed SQL broekn by rAthena update
//= 2.08 Fixed null Menus
//= 2.09 Condenced Menu Generation
//= 2.0A Reduce Reset PvP Stats size
//= 2.0B Reduce View Ranking size
//= 2.0C Remove "set" from script
//= 2.0D Remove Notes
//= 2.0E Fix Tabulation
//===== Todo: ================================================
//= [Stolao]
//= Add following KDA Confs in .Death
//= Kill - Death = Score
//= Kill (but show deaths)
//= fix menu generation to use only 1 line using "()?"
//= Remove Happy's notes
//===== Contact Ifo: =========================================
//= [Stolao]
// Email: Taingram11@gmail.com
//= [Happy]
// Email: http://hercules.ws/board/user/164-happy/
//============================================================
prontera,202,203,4	script	Ladder	837,{
	mes "[Ladder]";
OnPvpLadder:
	mes "Ranking";
	.@i = select(((.Death)?"Kill Ranking:Death Ranking":"View Ranking:")+":Leave:"+((getgmlevel() >= .GM)?"Reset PVP Stats":""));
	switch(.@i) { 
		case 4:
			mes "Are you sure you want to delete";
			mes "ALL pvp records?";
			if(select("No:Yes")==1) close;
			next;
			mes "Wait one Moment...";
			mes ".....";
			cleararray $TOPDTH$[0],0,127;
			cleararray $TOPDTH[0],0,127;
			cleararray $TOPPK$[0],0,127;
			cleararray $TOPPK[0],0,127;
			query_sql("DELETE FROM `char_reg_num` WHERE `key` = 'pl_pkcount' OR  `key` = 'pl_DTHcount'");
			.@self = getcharid(3);
			.@size = query_sql("SELECT account_id FROM `char` where online = 1", .@aid);
			for(.@i = 0; .@i<.@size; .@i++) {
				if(attachrid(.@aid[.@i])){
					pl_pkcount = 0;
					pl_DTHcount = 0;
				}
			}
			attachrid(.@self);
			mes "Cleared pvp data.";
			close;
		case 1:
		case 2:
			for(.@i = 1; .@i <= .MaxPlayers && .@i < getarraysize(((.@i == 1)?getarraysize($TOPPK):getarraysize($TOPDTH))) && .@i < getarraysize(((.@i == 1)?getarraysize($TOPPK$):getarraysize($TOPDTH$))); .@i++){
				mes (.@i)+": "+ ((.@i == 1)?$TOPPK$[.@i]:$TOPDTH$[.@i]) +" with "+ ((.@i == 1)?$TOPPK[.@i]:$TOPDTH[.@i]) +" kills";
				if((.@i) % 10 == 0){
					if(select("Read More:Leave") == 1)
						next;
					 else	close;
				}
			}
		case 3:
			close;
	}
close;
OnPCKillEvent:
	if(.Options&2 && (agitcheck() || agitcheck2()))
		end;
	if(getgmlevel() <= .GmLvl){
		if(.Options&1 || strcharinfo(3) == .Map$){
			if(.Options&32){
				for(.@i = .RepeatCount; .@i > 0; .@i--)
					@PKedID[.@i+1] = @PKedID[.@i];
				@PKedID = killedrid;
				for(.@i = 1; .@i < .RepeatCount; .@i++){
					if(@PKedID[0] == @PKedID[.@i])
						.@s += 1;
					 else	break;
				}
			}
			if(.@s < .RepeatCount){
				pl_pkcount = pl_pkcount + 1;
				if(.Options&16)
					set getd(.PointType$), getd(.PointType$) + .PvpEarned;
				if(pl_pkcount > $TOPPK[.MaxPlayers]) {
					for(.@i = 1; .@i <= .MaxPlayers ; .@i++) {
						if (pl_pkcount > $TOPPK[.@i] && !.@newrank)
							.@newrank = .@i;
						if (strcharinfo(0) == $TOPPK$[.@i] && .@newrank == .@i){
							$TOPPK$[.@i] = strcharinfo(0);
							$TOPPK[.@i] = pl_pkcount;
							.@newrank = 0;
							break;
						}
						if ( strcharinfo(0) == $TOPPK$[.@i] && .@newrank != .@i){
							.@existrank = .@i;
							break;
						}
					}
					if(.@newrank){
						for (.@e = .MaxPlayers; .@e >= 0 ; .@e -= 1){
							if(.@e < .@existrank){
								$TOPPK$[.@e + 1] = $TOPPK$[.@e];
								$TOPPK[.@e + 1] = $TOPPK[.@e];
							}
							if(.@e == .@newrank){
								$TOPPK$[.@e] = strcharinfo(0);
								$TOPPK[.@e] = pl_pkcount;
								if(.Options&4)
									announce "[PVP]: "+strcharinfo(0)+"'s kill rank is now "+(.@e)+"",bc_all;
								break;
							}
						}
					}
				}
				if(.Death){
					attachrid(@PKedID);
					pl_DTHcount = pl_DTHcount + 1;
					if(pl_DTHdthcount > $TOPDTH[.MaxPlayers]){
						for(.@i = 1; .@i <= .MaxPlayers ; .@i += 1) {
							if(pl_DTHcount > $TOPDTH[.@i] && !.@newrank)
								.@newrank = .@i;
							if(strcharinfo(0) == $TOPDTH$[.@i] && .@newrank == .@i){
								$TOPDTH$[.@i] = strcharinfo(0);
								$TOPDTH[.@i] = pl_DTHcount;
								.@newrank = 0;
								break;
							}
							if(strcharinfo(0) == $TOPDTH$[.@i] && .@newrank != .@i){
								.@existrank = .@i;
								break;
							}
						}
						if(.@newrank){
							for (.@e = .MaxPlayers; .@e >= 0 ; .@e -= 1) {
								if (.@e < .@existrank) {
									$TOPDTH$[.@e + 1] = $TOPDTH$[.@e];
									$TOPDTH[.@e + 1] = $TOPDTH[.@e];
								}
								if (.@e == .@newrank) {
									$TOPDTH$[.@e] = strcharinfo(0);
									$TOPDTH[.@e] = pl_DTHcount;
									if(.Options&4)
										announce "[PVP]: "+strcharinfo(0)+"'s death rank is now "+(.@e)+"",bc_all;
									break;
								}
							}
						}
					}
				}
			} else if(.Options&64)
				warp "Save",0,0;
		}
	}
	end;
OnInit:
	.GM = 99;
	.MaxPlayers = 10;		// Number of Players held in Records
	.GmLvl = 99; 			// Highest GM level Able to Hold a Record, if Option 8 is Disabled
	.Map$ = "prontera"; 		// Map Pvp Must Take Place on in order to Count, if Option 1 is Disabled
	.RepeatCount = 5; 		// Number of kills on same person before it detects abuse, max of 128
	.PointType$ = "#PvpPoints"; 	// Name of points earned, E.G. #Cash Points
	.PvpEarned = 1; 		// Number of Pvp Points Earned
	.Death = 0;			// Enable Death Ranking as well

	.Options = 4|32|64;
 		// Bitwise Variable
		// 1: Only Counts Kills on .Map$ if Disabled
		// 2: Disables Record Holding during WOE
		// 4: Announce When Someone Takes a Rank
		// 8: Allow Gms Above level .GmLvl To Be on Ladder
		// 16: Gain PvP Points on Kills (#PvpPoints)
		// 32: Repeat Kill Protection
		// 64: Warp to Save on abuse
		// Example: "1+2+4 = 7" No Forced Map, Disables Record Holding during WOE and Announces New Record Holders

	bindatcmd("PvpLadder" ,"Pvp_Ladder::OnPvpLadder",0,99);
end;
}
